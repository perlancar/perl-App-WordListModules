#!perl

# DATE
# VERSION

#use 5.010001;
use strict;
use warnings;

use Perinci::CmdLine::Any;
use Perinci::Sub::Gen::AccessTable qw(gen_read_table_func);
use Module::List::Tiny;

my $res = gen_read_table_func(
    name => 'app',
    summary => 'List WordList::* modules',
    table_data => sub {
        no strict 'refs';

        my $mods = Module::List::Tiny::list_modules(
            'WordList::', {list_modules=>1, recurse=>1});
        my @rows;
        for my $mod (sort keys %$mods) {
            $mod =~ /\AWordList::(.+)/;
            my $row = {wordlist=>$1};
            (my $mod_pm = "$mod.pm") =~ s!::!/!g;
            require $mod_pm;
            my $stats   = \%{"$mod\::STATS"};
            $row->{dynamic}    = ${"$mod\::DYNAMIC"} // 0;
            $row->{sort}       = ${"$mod\::SORT"};
            $row->{num_words}  = $stats->{num_words};
            $row->{num_params} = keys %{"$mod\::PARAMS"};
            push @rows, $row;
        }
        {data => \@rows};
    },
    table_spec => {
        fields => {
            wordlist => {
                schema => 'str*',
                pos => 0,
                sortable => 1,
            },
            sort => {
                schema => 'str',
                pos => 1,
                sortable => 1,
            },
            dynamic => {
                schema => 'uint',
                pos => 2,
                sortable => 1,
            },
            num_params => {
                schema => 'uint',
                pos => 3,
                sortable => 1,
            },
            num_words => {
                schema => 'uint',
                pos => 4,
                sortable => 1,
            },
        },
        pk => 'wordlist',
    },
);
die "Can't generate function: $res->[0] - $res->[1]" unless $res->[0] == 200;

Perinci::CmdLine::Any->new(url => '/main/app')->run;

# ABSTRACT:
# PODNAME:

=head1 SEE ALSO

L<wordlist> from L<App::wordlist>

L<WordList>
